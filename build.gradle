plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.4' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'com.diffplug.spotless' version "6.25.0" apply false
    id 'com.epages.restdocs-api-spec' version '0.19.2' apply false
}

allprojects {
    group = 'com.moyoy'
    version = '0.0.1-SNAPSHOT'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.diffplug.spotless'

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:2025.0.0"
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        implementation 'org.springframework.boot:spring-boot-starter'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }


    tasks.named('build') {
        dependsOn 'spotlessApply'
    }

    spotless {
        java {
            eclipse().configFile("${rootProject.projectDir}/config/spotless/naver-code-convention.xml")
            importOrder('java', 'javax' , 'lombok' , 'org', 'com', 'com.moyo', '')
            removeUnusedImports()
            trimTrailingWhitespace()
            endWithNewline()
        }
    }

    bootJar.enabled = false
    jar.enabled = false
}
